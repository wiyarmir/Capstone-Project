
apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibility
        targetCompatibility rootProject.ext.javaTargetCompatibility
    }

    defaultConfig {
        applicationId "es.guillermoorellana.travisforandroid"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }
}
dependencies {
    compile project(':libcore')
    compile project(':liblogic')
    compile project(':libservices')

    compile "com.jakewharton.timber:timber:4.5.1"

    // dagger2
    compile "com.google.dagger:dagger:2.11"
    annotationProcessor "com.google.dagger:dagger-compiler:2.11"
    compile "javax.inject:javax.inject:1"
    compile "javax.annotation:javax.annotation-api:1.2"

    // reactive family
    compile "io.reactivex:rxjava:1.3.0"
    compile "io.reactivex:rxandroid:1.2.1" // lazy guys not bumping...
    compile "com.artemzin.rxjava:proguard-rules:1.2.2.0"
    compile "com.jakewharton.rxbinding:rxbinding:0.4.0"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.3.0"

    compile "net.danlew:android.joda:2.9.0"

    // support libs
    compile "com.android.support:support-annotations:$supportLibsVersion"
    compile "com.android.support:appcompat-v7:$supportLibsVersion"
    compile "com.android.support:design:$supportLibsVersion"
    compile "com.android.support:recyclerview-v7:$supportLibsVersion"
    compile "com.android.support:cardview-v7:$supportLibsVersion"

    compile "com.jakewharton:butterknife:8.7.0"
    annotationProcessor "com.jakewharton:butterknife-compiler:8.7.0"

    compile "com.hannesdorfmann.mosby:mvp:2.0.0"
    compile "com.hannesdorfmann.mosby:viewstate:2.0.0"
    compile "com.hannesdorfmann.fragmentargs:annotation:3.0.0"
    annotationProcessor "com.hannesdorfmann.fragmentargs:processor:3.0.0"

    annotationProcessor "com.github.wiyarmir.DBFlow:dbflow-processor:$dbflowVersion"
    compile "com.github.wiyarmir.DBFlow:dbflow-core:$dbflowVersion"
    compile "com.github.wiyarmir.DBFlow:dbflow:$dbflowVersion"

    provided "javax.annotation:jsr250-api:1.0"
    provided "org.projectlombok:lombok:1.16.16"
    annotationProcessor "org.projectlombok:lombok:1.16.16"

    // Developer tools (Developer Settings)

    debugCompile "com.squareup.leakcanary:leakcanary-android:1.5"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5"

    debugCompile "com.facebook.stetho:stetho:1.3.1"
    debugCompile "com.facebook.stetho:stetho-okhttp:1.3.1"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:1.7.1"
    testCompile "nl.jqno.equalsverifier:equalsverifier:1.7.5"
    testCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testCompile "org.mockito:mockito-core:2.8.9"

    androidTestCompile "com.android.support.test:runner:1.0.0"
    androidTestCompile "com.android.support.test:rules:1.0.0"
    androidTestCompile "com.android.support.test.espresso:espresso-core:3.0.0"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:3.0.0") {
        // Causes IncompatibleClassChangeError. As alwaysâ€¦
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    }
    androidTestCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
}

configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force "com.android.support:support-annotations:$supportLibsVersion"
    }
}