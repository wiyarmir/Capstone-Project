/*
 * Copyright 2015 Guillermo Orellana Ruiz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'jacoco-everywhere'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibility
        targetCompatibility rootProject.ext.javaTargetCompatibility
    }

    defaultConfig {
        applicationId "es.guillermoorellana.travisforandroid"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }
}
dependencies {
    compile project(':libcore')
    compile project(':liblogic')
    compile project(':libservices')

    compile "com.jakewharton.timber:timber:4.1.0"

    // dagger2
    compile "com.google.dagger:dagger:2.5"
    apt "com.google.dagger:dagger-compiler:2.5"
    compile "javax.inject:javax.inject:1"
    compile "javax.annotation:javax.annotation-api:1.2"

    // reactive family
    compile "io.reactivex:rxjava:1.2.2"
    compile "io.reactivex:rxandroid:1.2.1" // lazy guys not bumping...
    compile "com.artemzin.rxjava:proguard-rules:1.2.2.0"
    compile "com.jakewharton.rxbinding:rxbinding:0.3.0"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.3.0"

    compile "net.danlew:android.joda:2.9.0"

    // support libs
    compile "com.android.support:support-annotations:$supportLibsVersion"
    compile "com.android.support:appcompat-v7:$supportLibsVersion"
    compile "com.android.support:design:$supportLibsVersion"
    compile "com.android.support:recyclerview-v7:$supportLibsVersion"
    compile "com.android.support:cardview-v7:$supportLibsVersion"

    compile "com.jakewharton:butterknife:8.4.0"
    apt "com.jakewharton:butterknife-compiler:8.4.0"

    compile "com.hannesdorfmann.mosby:mvp:2.0.0"
    compile "com.hannesdorfmann.mosby:viewstate:2.0.0"
    compile "com.hannesdorfmann.fragmentargs:annotation:3.0.0"
    apt "com.hannesdorfmann.fragmentargs:processor:3.0.0"

    apt "com.github.wiyarmir.DBFlow:dbflow-processor:$dbflowVersion"
    compile "com.github.wiyarmir.DBFlow:dbflow-core:$dbflowVersion"
    compile "com.github.wiyarmir.DBFlow:dbflow:$dbflowVersion"

    provided "javax.annotation:jsr250-api:1.0"
    provided "org.projectlombok:lombok:1.12.6"
    apt "org.projectlombok:lombok:1.12.6"

    // Developer tools (Developer Settings)

    debugCompile "com.squareup.leakcanary:leakcanary-android:1.5"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.5"

    debugCompile "com.facebook.stetho:stetho:1.3.1"
    debugCompile "com.facebook.stetho:stetho-okhttp:1.3.1"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:1.7.1"
    testCompile "nl.jqno.equalsverifier:equalsverifier:1.7.5"
    testCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testCompile "org.mockito:mockito-core:1.10.19"

    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        // Causes IncompatibleClassChangeError. As alwaysâ€¦
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    }
    androidTestCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
}

configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force "com.android.support:support-annotations:$supportLibsVersion"
    }
}