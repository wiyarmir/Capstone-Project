/*
 * Copyright 2015 Guillermo Orellana Ruiz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.fernandocejas.frodo'
apply plugin: 'jacoco-everywhere'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibility
        targetCompatibility rootProject.ext.javaTargetCompatibility
    }

    defaultConfig {
        applicationId "es.guillermoorellana.travisforandroid"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug{
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        ignoreWarnings true

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }
}
dependencies {
    compile project(':libcore')
    compile project(':liblogic')
    compile project(':libservices')

    compile libraries.app.timber
    compile libraries.app.dagger
    apt libraries.app.daggerCompiler
    compile libraries.app.javaxInject
    compile libraries.app.javaxAnnotationApi

    compile libraries.app.rxJava
    compile libraries.app.rxAndroid
    compile libraries.app.rxAndroidProguard
    compile libraries.app.rxBinding
    compile libraries.app.rxBindingV7

    provided libraries.app.autoValue
    compile libraries.app.jodaTimeAndroid

    compile libraries.app.supportAnnotations
    compile libraries.app.supportAppCompat
    compile libraries.app.supportDesign
    compile libraries.app.supportRecyclerView
    compile libraries.app.supportCardView

    apt libraries.app.butterKnifeCompiler
    compile libraries.app.butterKnife

    compile libraries.app.picasso
    compile libraries.app.mosby
    compile libraries.app.mosbyViewstate
    compile libraries.app.fragmentargs
    apt libraries.app.fragmentargsProcessor

    apt libraries.app.dbflowProcessor
    compile libraries.app.dbflowCore
    compile libraries.app.dbflow

    provided libraries.app.jsr250
    provided libraries.app.lombok
    apt libraries.app.lombok

    // Developer tools (Developer Settings)

    debugCompile libraries.app.leakCanary
    releaseCompile libraries.app.leakCanaryNoOp
    testCompile libraries.app.leakCanaryNoOp

    debugCompile libraries.app.stetho
    debugCompile libraries.app.stethoOkHttp

    testCompile libraries.test.junit
    testCompile libraries.test.assertJ
    testCompile libraries.test.equalsVerifier
    testCompile libraries.test.okHttpMockWebServer
    testCompile libraries.test.mockitoCore

    androidTestCompile libraries.test.supportTestRunner
    androidTestCompile libraries.test.supportTestRules
    androidTestCompile libraries.test.espressoCore
    androidTestCompile(libraries.test.espressoContrib) {
        // Causes IncompatibleClassChangeError. As alwaysâ€¦
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    }
    androidTestCompile libraries.test.okHttpMockWebServer
}

configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force libraries.app.supportAnnotations
    }
}