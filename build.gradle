
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha8'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }

        maven {
            url "https://jitpack.io"
        }

        maven {
            url 'https://clojars.org/repo/'
        }
    }
    tasks.withType(JavaForkOptions) {
        jvmArgs '-Djava.awt.headless=true'
    }
}

ext {
    minSdk = 21
    targetSdk = 26
    compileSdkVersion = 26
    buildToolsVersion = '26.0.0'

    preDexLibs = !project.hasProperty('disablePreDex')

    javaSourceCompatibility = JavaVersion.VERSION_1_8
    javaTargetCompatibility = JavaVersion.VERSION_1_8

    supportLibsVersion = '26.0.0'
    okHttpVersion = '3.2.0'
    retrofitVersion = '2.3.0'
    dbflowVersion = 'fix'
}

subprojects {
    apply plugin: 'pmd'

    pmd {
        ignoreFailures = false // Fail early.
        ruleSets = ['java-basic', 'java-braces', 'java-strings', 'java-design', 'java-unusedcode']
    }

    apply plugin: 'findbugs'

    findbugs {
        ignoreFailures = false // Fail early.
        effort = 'default'
        reportLevel = 'low' // Report even low priority problems.


        excludeFilter = rootProject.file('findbugs-filter.xml')
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    apply plugin: 'checkstyle'

    checkstyle {
        configFile rootProject.file('checkstyle.xml')

        ignoreFailures false // Fail early.
        showViolations true
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled true
            html.enabled true
            html.stylesheet resources.text.fromFile(rootProject.file('config/checkstyle-noframes-sorted.xsl'))
        }
    }
}
